plugins {
    id "com.palantir.docker" version "0.25.0"
    id "de.undercouch.download" version "4.1.1"
    id "distribution"
}

dependencies {
    compile project(':situp-core')
}

task endToEndTests {
    //dependsOn(':situp-api:test') TODO add E2E and enable when ready
}

task benchmarkTests {
    //dependsOn(':situp-api:test') TODO add benchmark test and enable
}

task buildCore {
    dependsOn ':situp-core:build'
}

task releasePrerequisites {
    dependsOn 'buildCore'
    dependsOn 'endToEndTests'
    dependsOn 'benchmarkTests'
}

docker {
    name "situp/${project.rootProject.name}:${project.version}"
    tag "${project.rootProject.name}", "${project.version}" //update tag when ready to push
    files project(':situp-core').jar.archivePath
    buildArgs(['JAR_FILE'       : project(':situp-core').jar.archiveName,
               'CONFIG_FILEPATH': '/usr/share/situp/situp.yml'])
    dockerfile file('docker/Dockerfile')
}

dockerPrepare.dependsOn releasePrerequisites
dockerPush.dependsOn docker //TODO Add dockerPush specific task when ready to publish

task downloadJDK() {
    mkdir "${buildDir}/openjdk"
    doLast {
        download {
            //TODO determine version from versions file
            //Adding JDK14 but jar can be executed by JDK1.8 as well
            src 'https://download.java.net/openjdk/jdk14/ri/openjdk-14+36_linux-x64_bin.tar.gz'
            dest "${buildDir}/openjdk/openjdk.tar.gz"
            overwrite false
        }
    }
}
CopySpec archiveToTar(platform) {
    return copySpec {
        if (!file("archives/${platform}").exists()) {
            platform = project.property('default-release-platform')
        }
        into('bin') {
            from project(':situp-core').jar.archivePath
            fileMode 0755
        }
        into('examples') {
            from("${rootDir}/examples")
            dirMode 0750
            fileMode 0755
        }
        into('config') {
            from("${rootDir}/shared-resources/log4j.properties")
        }
        into('') {
            from("${rootDir}/LICENSE")
            from("${rootDir}/NOTICE")
            from("archives/${platform}/situp-tar-install.sh")
            fileMode 0755
        }
    }
}

distributions {
    linux {
        distributionBaseName = "${project.rootProject.name}-linux-x86_64"
        contents {
            with archiveToTar('linux-x86_64')
        }
    }
    linuxWithJDK {
        distributionBaseName = "${project.rootProject.name}-linux-x86_64-jdk"
        contents {
            with distributions.linux.contents
            into('openjdk') {
                from tarTree("${buildDir}/openjdk/openjdk.tar.gz")
            }
        }
    }
}

tasks.withType(Tar) {
    dependsOn releasePrerequisites
    dependsOn downloadJDK
    compression = Compression.GZIP
    extension = 'tar.gz'
}

tasks.withType(Zip) {
    dependsOn releasePrerequisites
    dependsOn downloadJDK
}