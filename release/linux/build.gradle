plugins {
    id "de.undercouch.download" version "4.1.1"
    id "distribution"
}
def platform = "linux_x86_64"
distributions {
    linux {
        distributionBaseName = "${project.rootProject.name}-${platform}"
        contents {
            with archiveToTar("${platform}")
            into('') {
                from("situp-tar-install.sh")
                fileMode 0755
            }
        }
    }
    linuxWithJDK {
        distributionBaseName = "${project.rootProject.name}-${platform}-jdk"
        contents {
            with archiveToTar("${platform}")
            into('openjdk') {
                from tarTree("${buildDir}/${platform}/openjdk/openjdk.tar.gz")
            }
            into('') {
                from("situp-tar-install-with-jdk.sh").rename("situp-tar-install-with-jdk.sh", "situp-tar-install.sh")
                fileMode 0755
            }
        }
    }
}

task downloadJDK {
    doLast {
        download {
            src jdkSources[platform]
            dest "${buildDir}/${platform}/openjdk/openjdk.tar.gz"
            overwrite false
        }
    }
}
task linuxTar {
    dependsOn linuxDistTar
    dependsOn linuxWithJDKDistTar
}

task linuxZip {
    dependsOn linuxDistZip
    dependsOn linuxWithJDKDistZip
}

linuxWithJDKDistTar.dependsOn downloadJDK
linuxWithJDKDistZip.dependsOn downloadJDK
