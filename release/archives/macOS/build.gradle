import com.amazonaws.services.s3.model.ObjectMetadata
import jp.classmethod.aws.gradle.s3.AmazonS3FileUploadTask
import jp.classmethod.aws.gradle.s3.CreateBucketTask

def platform = "macOS_x64"
distributions {
    macOS {
        distributionBaseName = "${project.rootProject.name}-${platform}"
        contents {
            with archiveToTar()
            into('') {
                from("situp-tar-install.sh")
                fileMode 0755
            }
        }
    }
    macOSWithJDK {
        distributionBaseName = "${project.rootProject.name}-${platform}-jdk"
        contents {
            with archiveToTar()
            into('openjdk') {
                from tarTree("${buildDir}/${platform}/openjdk/openjdk.tar.gz")
            }
            into('') {
                from("situp-tar-install-with-jdk.sh").rename("situp-tar-install-with-jdk.sh", "situp-tar-install.sh")
                fileMode 0755
            }
        }
    }
}

task downloadJDK {
    doLast {
        download {
            src jdkSources[platform]
            dest "${buildDir}/${platform}/openjdk/openjdk.tar.gz"
            overwrite false
        }
    }
}
task createBucket(type: CreateBucketTask) {
    bucketName awsS3Bucket
    ifNotExists true
}

task uploadTarToS3 (type: AmazonS3FileUploadTask, dependsOn: createBucket) {
    dependsOn macOSDistTar
    file file(macOSDistTar.archiveFile.get().asFile.absolutePath)
    bucketName awsS3Bucket
    key macOSDistTar.archiveName

    def m = new ObjectMetadata()
    m.setCacheControl("no-cache, no-store")
    objectMetadata = m
}
task uploadTarWithJDKToS3 (type: AmazonS3FileUploadTask, dependsOn: createBucket) {
    dependsOn macOSWithJDKDistTar
    file file(macOSWithJDKDistTar.archiveFile.get().asFile.absolutePath)
    bucketName awsS3Bucket
    key macOSWithJDKDistTar.archiveName

    def m = new ObjectMetadata()
    m.setCacheControl("no-cache, no-store")
    objectMetadata = m
}
task uploadToS3 {
    dependsOn uploadTarToS3
    dependsOn uploadTarWithJDKToS3
}

task macOSTar {
    dependsOn macOSDistTar
    dependsOn macOSWithJDKDistTar
}

task macOSZip {
    dependsOn macOSDistZip
    dependsOn macOSWithJDKDistZip
}

macOSWithJDKDistTar.dependsOn downloadJDK
macOSWithJDKDistZip.dependsOn downloadJDK