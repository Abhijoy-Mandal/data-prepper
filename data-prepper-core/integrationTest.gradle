buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:6.6.1'
    }
}

apply plugin: 'com.bmuschko.docker-remote-api'

/*
 *  SPDX-License-Identifier: Apache-2.0
 *
 *  The OpenSearch Contributors require contributions made to
 *  this file be licensed under the Apache-2.0 license or a
 *  compatible open source license.
 *
 *  Modifications Copyright OpenSearch Contributors. See
 *  GitHub history for details.
 */

import com.bmuschko.gradle.docker.tasks.container.*
import com.bmuschko.gradle.docker.tasks.image.*
import com.bmuschko.gradle.docker.tasks.network.*

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }
}

repositories {
    mavenLocal()
}

dependencies {
    integrationTestCompile("junit:junit:4.13")
    integrationTestCompile project(':data-prepper-plugins:elasticsearch')
    integrationTestImplementation "org.awaitility:awaitility:4.0.3"
    integrationTestImplementation "io.opentelemetry:opentelemetry-proto:${versionMap.opentelemetry_proto}"
    integrationTestImplementation 'com.google.protobuf:protobuf-java-util:3.13.0'
    integrationTestImplementation "com.linecorp.armeria:armeria:1.0.0"
    integrationTestImplementation "com.linecorp.armeria:armeria-grpc:1.0.0"
    integrationTestImplementation "org.opensearch.client:opensearch-rest-high-level-client:${versionMap.es_version}-SNAPSHOT"
    integrationTestImplementation "com.fasterxml.jackson.core:jackson-databind:2.12.1"
}

/**
 * End-to-end test docker network
 */
task createDataPrepperNetwork(type: DockerCreateNetwork) {
    networkName = "data_prepper_network"
}

task removeDataPrepperNetwork(type: DockerRemoveNetwork) {
    dependsOn createDataPrepperNetwork
    networkId = createDataPrepperNetwork.getNetworkId()
}

/**
 * DataPrepper Docker tasks
 */
task createDataPrepperDockerFile(type: Dockerfile) {
    dependsOn jar
    destFile = project.file('build/docker/Dockerfile')
    from("adoptopenjdk/openjdk14:jre-14.0.1_7-alpine")
    exposePort(21890)
    exposePort(4900)
    workingDir("/app")
    copyFile("build/libs/${jar.archiveName}", "/app/data-prepper.jar")
    copyFile("src/integrationTest/resources/pipeline.yml", "/app/pipeline.yml")
    copyFile("src/integrationTest/resources/data_prepper.yml", "/app/data_prepper.yml")
    defaultCommand("java", "-jar", "data-prepper.jar", "/app/pipeline.yml", "/app/data_prepper.yml")
}

task buildDataPrepperDockerImage(type: DockerBuildImage) {
    dependsOn createDataPrepperDockerFile
    inputDir  = file(".")
    dockerFile  = file("build/docker/Dockerfile")
    images.add("integ-test-pipeline-image")
}

task createDataPrepper1DockerContainer(type: DockerCreateContainer) {
    dependsOn buildDataPrepperDockerImage
    dependsOn createDataPrepperNetwork
    containerName = "data-prepper1"
    hostConfig.portBindings = ['21890:21890', '4900:4900']
    hostConfig.network = createDataPrepperNetwork.getNetworkName()
    targetImageId buildDataPrepperDockerImage.getImageId()
}

task createDataPrepper2DockerContainer(type: DockerCreateContainer) {
    dependsOn buildDataPrepperDockerImage
    dependsOn createDataPrepperNetwork
    containerName = "data-prepper2"
    hostConfig.portBindings = ['21891:21890', '4901:4900']
    hostConfig.network = createDataPrepperNetwork.getNetworkName()
    targetImageId buildDataPrepperDockerImage.getImageId()
}

task startDataPrepper1DockerContainer(type: DockerStartContainer) {
    dependsOn createDataPrepper1DockerContainer
    targetContainerId createDataPrepper1DockerContainer.getContainerId()
    doLast {
        sleep(10*1000)
    }
}

task startDataPrepper2DockerContainer(type: DockerStartContainer) {
    dependsOn createDataPrepper2DockerContainer
    targetContainerId createDataPrepper2DockerContainer.getContainerId()
    doLast {
        sleep(10*1000)
    }
}

task stopDataPrepper1DockerContainer(type: DockerStopContainer) {
    targetContainerId createDataPrepper1DockerContainer.getContainerId()
}

task stopDataPrepper2DockerContainer(type: DockerStopContainer) {
    targetContainerId createDataPrepper2DockerContainer.getContainerId()
}

task removeDataPrepper1DockerContainer(type: DockerRemoveContainer) {
    targetContainerId stopDataPrepper1DockerContainer.getContainerId()
}

task removeDataPrepper2DockerContainer(type: DockerRemoveContainer) {
    targetContainerId stopDataPrepper2DockerContainer.getContainerId()
}

/**
 * ODFE Docker tasks
 */
task pullOdfeDockerImage(type: DockerPullImage) {
    image = 'amazon/opendistro-for-elasticsearch:1.13.2'
}

task createOdfeDockerContainer(type: DockerCreateContainer) {
    dependsOn createDataPrepperNetwork
    dependsOn pullOdfeDockerImage
    targetImageId pullOdfeDockerImage.image
    containerName = "node-0.example.com"
    hostConfig.portBindings = ['9200:9200', '9600:9600']
    hostConfig.autoRemove = true
    hostConfig.network = createDataPrepperNetwork.getNetworkName()
    envVars = ['discovery.type':'single-node']
}

task startOdfeDockerContainer(type: DockerStartContainer) {
    dependsOn createOdfeDockerContainer
    targetContainerId createOdfeDockerContainer.getContainerId()

    doLast {
        sleep(90*1000)
    }
}

task stopOdfeDockerContainer(type: DockerStopContainer) {
    targetContainerId createOdfeDockerContainer.getContainerId()
}

/**
 * End to end test. Spins up ODFE and DataPrepper docker containers, then runs the integ test
 * Stops the docker containers when finished
 */
task rawSpanEndToEndTest(type: Test) {
    dependsOn build
    dependsOn startOdfeDockerContainer
    dependsOn startDataPrepper1DockerContainer
    startDataPrepper1DockerContainer.mustRunAfter 'startOdfeDockerContainer'

    description = 'Runs the raw span integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    filter {
        includeTestsMatching "com.amazon.dataprepper.integration.EndToEndRawSpanTest*"
    }

    finalizedBy stopOdfeDockerContainer
    finalizedBy stopDataPrepper1DockerContainer
    finalizedBy removeDataPrepper1DockerContainer
    finalizedBy removeDataPrepperNetwork
}

task serviceMapEndToEndTest(type: Test) {
    dependsOn build
    dependsOn startOdfeDockerContainer
    dependsOn startDataPrepper1DockerContainer
    dependsOn startDataPrepper2DockerContainer
    startDataPrepper1DockerContainer.mustRunAfter 'startOdfeDockerContainer'
    startDataPrepper2DockerContainer.mustRunAfter 'startOdfeDockerContainer'

    description = 'Runs the raw span integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    filter {
        includeTestsMatching "com.amazon.dataprepper.integration.EndToEndServiceMapTest*"
    }

    finalizedBy stopOdfeDockerContainer
    finalizedBy stopDataPrepper1DockerContainer
    finalizedBy stopDataPrepper2DockerContainer
    finalizedBy removeDataPrepper1DockerContainer
    finalizedBy removeDataPrepper2DockerContainer
    finalizedBy removeDataPrepperNetwork
}
